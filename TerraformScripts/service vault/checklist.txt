Requirements
-Source VM network
-Destination VM network
-Source Virtual Machines
-Source Storage account
-Cache Storage Account


Components of Site Recovery Services
-Object in the vault that represents the azure region where vms are protected
-Only one fabric object can be created per region.
-If enabled replication from portal, it gets created automatically
-You cant create new one if already exist

Protection Container
-Container used to group replicated items within a Fabric (Primary Conatiner & Recovery Container)


Proctection Container Mapping
-Maps the Primary Proctection Conatiner with a recovery Protection Container
-For reverse replication the roles of the original region and the recovery region
 switch. The original region now becomes a new recovery region and viceversa

Cache Storage account
-Holds replication changes temporarily
-Azure creates one storage cache if not create by You

Network Mapping
-This maps primary region vnet to disaster recovery vnet in secondary region





*******************************************************************************************************

resource "azurerm_recovery_services_vault" "vault" {
  name                = var.recover_vault_name
  location            = var.recovery_vault_location
  resource_group_name = var.recovery_vault_rg
  sku                 = "Standard"
}

resource "azurerm_site_recovery_fabric" "primary" {
  name                = "primary-fabric"
  resource_group_name = var.recovery_primary_rg
  recovery_vault_name = azurerm_recovery_services_vault.vault.name
  location            = var.recovery_primary_location
}

resource "azurerm_site_recovery_fabric" "secondary" {
  name                = "secondary-fabric"
  resource_group_name = var.recovery_secondary_rg
  recovery_vault_name = azurerm_recovery_services_vault.vault.name
  location            = var.recovery_secondary_location
}

resource "azurerm_site_recovery_protection_container" "primary" {
  name                 = "primary-protection-container"
  resource_group_name  = var.recovery_primary_rg
  recovery_vault_name  = azurerm_recovery_services_vault.vault.name
  recovery_fabric_name = azurerm_site_recovery_fabric.primary.name
}

resource "azurerm_site_recovery_protection_container" "secondary" {
  name                 = "secondary-protection-container"
  resource_group_name  = var.recovery_secondary_rg
  recovery_vault_name  = azurerm_recovery_services_vault.vault.name
  recovery_fabric_name = azurerm_site_recovery_fabric.secondary.name
}

resource "azurerm_site_recovery_replication_policy" "policy" {
  name                                                 = "policy"
  resource_group_name                                  = var.recovery_secondary_rg
  recovery_vault_name                                  = azurerm_recovery_services_vault.vault.name
  recovery_point_retention_in_minutes                  = 24 * 60
  application_consistent_snapshot_frequency_in_minutes = 4 * 60
}

resource "azurerm_site_recovery_protection_container_mapping" "container-mapping" {
  name                                      = "container-mapping"
  resource_group_name                       = var.recovery_secondary_rg
  recovery_vault_name                       = azurerm_recovery_services_vault.vault.name
  recovery_fabric_name                      = azurerm_site_recovery_fabric.primary.name
  recovery_source_protection_container_name = azurerm_site_recovery_protection_container.primary.name
  recovery_target_protection_container_id   = azurerm_site_recovery_protection_container.secondary.id
  recovery_replication_policy_id            = azurerm_site_recovery_replication_policy.policy.id
}

resource "azurerm_site_recovery_network_mapping" "network-mapping" {
  name                        = "network-mapping"
  resource_group_name         = var.recovery_secondary_rg
  recovery_vault_name         = azurerm_recovery_services_vault.vault.name
  source_recovery_fabric_name = azurerm_site_recovery_fabric.primary.name
  target_recovery_fabric_name = azurerm_site_recovery_fabric.secondary.name
  source_network_id           = module.vnet.primary_vnet.id
  target_network_id           = module.vnet_secondary.secondary_vnet.id
}

resource "azurerm_storage_account" "primary" {
  name                     = "primaryrecoverycache3248"
  location                 = var.recovery_primary_location
  resource_group_name      = var.recovery_primary_rg
  account_tier             = "Standard"
  account_replication_type = "LRS"
}


resource "azurerm_site_recovery_replicated_vm" "vm-replication" {
  name                                      = "vm-replication"
  resource_group_name                       = azurerm_resource_group.secondary.name
  recovery_vault_name                       = azurerm_recovery_services_vault.vault.name
  source_recovery_fabric_name               = azurerm_site_recovery_fabric.primary.name
  source_vm_id                              = azurerm_virtual_machine.vm.id
  recovery_replication_policy_id            = azurerm_site_recovery_replication_policy.policy.id
  source_recovery_protection_container_name = azurerm_site_recovery_protection_container.primary.name

  target_resource_group_id                = azurerm_resource_group.secondary.id
  target_recovery_fabric_id               = azurerm_site_recovery_fabric.secondary.id
  target_recovery_protection_container_id = azurerm_site_recovery_protection_container.secondary.id

  managed_disk {
    disk_id                    = azurerm_virtual_machine.vm.storage_os_disk[0].managed_disk_id
    staging_storage_account_id = azurerm_storage_account.primary.id
    target_resource_group_id   = azurerm_resource_group.secondary.id
    target_disk_type           = "Premium_LRS"
    target_replica_disk_type   = "Premium_LRS"
  }

  network_interface {
    source_network_interface_id   = azurerm_network_interface.vm.id
    target_subnet_name            = azurerm_subnet.secondary.name
    recovery_public_ip_address_id = azurerm_public_ip.secondary.id
  }

  depends_on = [
    azurerm_site_recovery_protection_container_mapping.container-mapping,
    azurerm_site_recovery_network_mapping.network-mapping,
  ]
}

